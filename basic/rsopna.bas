100 CONSOLE ,,0
110 OPEN "COM:N81" AS #1
120 AD=&HAC:DT=&HAD
130 INPUT #1,C
140 IF C=1 THEN GOSUB *OPW
150 IF C=2 THEN GOSUB *OPR
160 IF C=3 THEN GOSUB *OPS
170 IF C=4 THEN PRINT #1,""
180 IF C=5 THEN GOSUB *PAUSE
190 IF C=10 THEN GOSUB *WRM
200 IF C=11 THEN GOSUB *RDM
210 IF C=99 THEN GOTO 240
220 PRINT #1,"@";
230 GOTO 130
240 END
250 '
260 *OPW
270 INPUT #1,A$,D$
280 A=VAL("&H"+A$):D=VAL("&H"+D$)
290 OUT AD,A:OUT DT,D
300 PRINT #1,"O"+RIGHT$("0"+HEX$(A),2)+":"+RIGHT$("0"+HEX$(D),2)+" ";
310 RETURN
320 '
330 *OPR
340 INPUT #1,A$
350 A=VAL("&H"+A$)
360 OUT AD,A:D=INP(DT)
370 PRINT #1,"I"+RIGHT$("0"+HEX$(A),2)+":"+RIGHT$("0"+HEX$(D),2)+" ";
380 RETURN
390 '
400 *OPS
410 S=INP(AD)
420 PRINT #1,"S"+RIGHT$("0"+HEX$(S),2)+"    ";
430 RETURN
440 *PAUSE
450 INPUT #1,M$
460 PRINT #1,""
470 PRINT #1,M$
480 CAPS=INP(10) AND 128
490 'IF CAPS=0 THEN PRINT ELSE INPUT DUMMY$
500 RETURN
510 '
520 *WRM
530 INPUT #1,D$
540 D=VAL("&H"+D$)
550 INPUT #1,R
560 FOR I=1 TO R
570   OUT AD,&H8:OUT DT,D
580   'GOSUB *W
590   S1=INP(AD)
600   OUT AD,&H10:OUT DT,&H80
610   PRINT #1,"W"+RIGHT$("0"+HEX$(D),2)+":"+RIGHT$("0"+HEX$(S1),2);
620   PRINT #1," ";
630   D=D+1
640 NEXT
650 RETURN
660 '
670 *RDM
680 INPUT #1,R
690 FOR I=1 TO R
700   OUT AD,&H8:D=INP(DT)
710   'GOSUB *W
720   S1=INP(AD)
730   OUT AD,&H10:OUT DT,&H80
740   PRINT #1,"R"+RIGHT$("0"+HEX$(D),2)+":"+RIGHT$("0"+HEX$(S1),2);
750   PRINT #1," ";
760 NEXT
770 RETURN
780 *W
790 FOR WWW=0 TO 3: NEXT: RETURN
