10 CONSOLE ,,0
20 OPEN "COM:N81" AS #1
30 AD=&HAC:DT=&HAD
40 INPUT #1,C
50 IF C=1 THEN GOSUB *OPW
60 IF C=2 THEN GOSUB *OPR
70 IF C=3 THEN GOSUB *OPS
80 IF C=4 THEN PRINT #1,""
90 IF C=5 THEN GOSUB *PAUSE
100 IF C=10 THEN GOSUB *WRM
110 IF C=11 THEN GOSUB *RDM
120 IF C=99 THEN GOTO 140
125 PRINT #1,"@";
130 GOTO 40
140 END
150 '
160 *OPW
170 INPUT #1,A$,D$
180 A=VAL("&H"+A$):D=VAL("&H"+D$)
190 OUT AD,A:OUT DT,D
200 PRINT #1,"O"+RIGHT$("0"+HEX$(A),2)+":"+RIGHT$("0"+HEX$(D),2)+" ";
210 RETURN
220 '
230 *OPR
240 INPUT #1,A$
250 A=VAL("&H"+A$)
260 OUT AD,A:D=INP(DT)
270 PRINT #1,"I"+RIGHT$("0"+HEX$(A),2)+":"+RIGHT$("0"+HEX$(D),2)+" ";
280 RETURN
290 '
300 *OPS
310 S=INP(AD)
320 PRINT #1,"S"+RIGHT$("0"+HEX$(S),2)+"    ";
330 RETURN
340 *PAUSE
350 INPUT #1,M$
355 PRINT #1,""
360 PRINT #1,M$
370 CAPS=INP(10) AND 128
380 'IF CAPS=0 THEN PRINT ELSE INPUT DUMMY$
390 RETURN
400 '
410 *WRM
420 INPUT #1,D$
430 D=VAL("&H"+D$)
440 INPUT #1,R
450 FOR I=1 TO R
460   OUT AD,&H8:OUT DT,D
470   'GOSUB *W
480   S1=INP(AD)
490   OUT AD,&H10:OUT DT,&H80
500   PRINT #1,"W"+RIGHT$("0"+HEX$(D),2)+":"+RIGHT$("0"+HEX$(S1),2);
510   PRINT #1," ";
520   D=D+1
530 NEXT
540 RETURN
550 '
560 *RDM
570 INPUT #1,R
580 FOR I=1 TO R
590   OUT AD,&H8:D=INP(DT)
600   'GOSUB *W
610   S1=INP(AD)
620   OUT AD,&H10:OUT DT,&H80
630   PRINT #1,"R"+RIGHT$("0"+HEX$(D),2)+":"+RIGHT$("0"+HEX$(S1),2);
640   PRINT #1," ";
650 NEXT
660 RETURN
670 *W
680 FOR WWW=0 TO 3: NEXT: RETURN
